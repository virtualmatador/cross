cmake_minimum_required(VERSION 3.18)
include("../../../project.cmake")
project(${project_name} LANGUAGES NONE VERSION ${project_version})
enable_testing()
set(html_dir "${CMAKE_CURRENT_BINARY_DIR}/www/html")
set_property(DIRECTORY APPEND PROPERTY
    CMAKE_CONFIGURE_DEPENDS "${CMAKE_CURRENT_LIST_DIR}/../../../html")
file(GLOB assets "${CMAKE_CURRENT_LIST_DIR}/../../../html/*")
file(GLOB extra_assets RELATIVE "${html_dir}" "${html_dir}/*")
file(WRITE "${html_dir}/manifest.appcache" "CACHE MANIFEST\nCACHE:\n")
foreach(asset ${assets})
    file(READ "${asset}" content)
    string(STRIP "${content}" content)
    get_filename_component(base "${asset}" NAME)
    if(${base} MATCHES ".+\.png$")
        separate_arguments(content)
        list(GET content 0 png_source)
        list(GET content 1 png_width)
        list(GET content 2 png_height)
        set(png_source "${CMAKE_CURRENT_LIST_DIR}/../../../${png_source}")
        add_custom_target("image-${base}" ALL DEPENDS "${html_dir}/${base}")
        add_custom_command(OUTPUT "${html_dir}/${base}"
            COMMAND "inkscape" -o "${html_dir}/${base}"
                -w ${png_width} -h ${png_height} "${png_source}"
            MAIN_DEPENDENCY "${asset}"
            DEPENDS "${png_source}")
    elseif(${base} MATCHES ".+\.lnk$")
        string(REGEX REPLACE "\.lnk$" "" base ${base})
        add_custom_target("lnk-${base}" ALL DEPENDS "${html_dir}/${base}")
        add_custom_command(OUTPUT "${html_dir}/${base}"
            COMMAND ln -s "${asset_dir}/${content}" "${html_dir}/${base}"
            MAIN_DEPENDENCY "${asset}")
    elseif(${base} MATCHES ".+\.wav$")
        configure_file("${asset}" "${html_dir}/${base}" COPYONLY)
    else()
        configure_file("${asset}" "${html_dir}/${base}" @ONLY)
    endif()
    list(REMOVE_ITEM extra_assets ${base})
endforeach()
list(REMOVE_ITEM extra_assets "manifest.appcache")
foreach(asset ${extra_assets})
    file(REMOVE "${html_dir}/${asset}")
endforeach()
