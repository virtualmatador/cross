cmake_minimum_required(VERSION 3.18)
include("../../../project.cmake")
project(${project_name} LANGUAGES NONE VERSION ${project_version})
enable_testing()
set(html_dir "${CMAKE_CURRENT_BINARY_DIR}/www/html")
set_property(DIRECTORY APPEND PROPERTY
    CMAKE_CONFIGURE_DEPENDS "${CMAKE_CURRENT_LIST_DIR}/../../../html")
file(GLOB assets "${CMAKE_CURRENT_LIST_DIR}/../../../html/*")
file(GLOB extra_assets RELATIVE "${html_dir}" "${html_dir}/*")
file(WRITE "${html_dir}/manifest.appcache" "CACHE MANIFEST\nCACHE:\n")
foreach(asset ${assets})
    get_filename_component(base "${asset}" NAME)
    if(${base} MATCHES ".+\.png$")
        file(READ "${asset}" switches)
        separate_arguments(switches)
        list(GET switches 0 png_source)
        list(GET switches 1 png_width)
        list(GET switches 2 png_height)
        set(png_source "${CMAKE_CURRENT_LIST_DIR}/../../../${png_source}")
        add_custom_command(OUTPUT "${html_dir}/${base}"
            COMMAND "inkscape" -o "${html_dir}/${base}"
                -w ${png_width} -h ${png_height} "${png_source}"
            MAIN_DEPENDENCY "${png_source}")
        add_custom_target("image-${base}" ALL DEPENDS "${html_dir}/${base}")
    elseif(${asset} MATCHES ".+\.wav$")
        configure_file("${asset}" "${html_dir}/${base}" COPYONLY)
    else()
        configure_file("${asset}" "${html_dir}/${base}")
    endif()
    list(REMOVE_ITEM extra_assets ${base})
endforeach()
list(REMOVE_ITEM extra_assets "manifest.appcache")
foreach(asset ${extra_assets})
    file(REMOVE "${html_dir}/${asset}")
endforeach()
